apply plugin: 'com.android.library'
apply plugin: 'android-maven'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion 28
    defaultConfig {
        def versionNameList = "$application_version".split(/\./)
        def powCounter = versionNameList.size()
        def versionNumbers = versionNameList.collect{numberString -> numberString.toInteger()}
        def versionNumber = versionNumbers.sum{ number ->
            powCounter = powCounter - 1
            (Math.pow(100, powCounter) * number).toInteger()
        }
        minSdkVersion 14
        targetSdkVersion 28
        versionCode versionNumber
        versionName "$application_version"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

publish {
    userOrg = 'takukobayashi'
    repoName ='RuntimePermissionChecker'
    groupId = 'net.taptappun.taku.kobayashi'
    artifactId = 'runtimepermissionchecker'
    publishVersion = "$application_version"
    desc = 'I made it possible to distribute it from Bitray'
    website = 'https://github.com/TakuKobayashi/RuntimePermissionChecker'
}

ktlint {
    version = "0.34.2"
    android = true
    ignoreFailures = true
    additionalEditorconfigFile = file(".editorconfig")
}

dokka {
    outputFormat = 'html'
    sourceDirs = files('src/main')
    outputDirectory = "$buildDir/documents"
}